plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.1.6'
    id 'maven-publish'
    // https://github.com/researchgate/gradle-release
    id 'net.researchgate.release' version '3.0.2'
    // https://github.com/JetBrains/gradle-changelog-plugin/
    id 'org.jetbrains.changelog' version '2.1.2'
}

group 'org.sfvl.doctesting'

repositories {
    mavenCentral()
    flatDir {
        dirs 'lib' // Only for documentationtesting while waiting for it to be in Maven central.
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:2.1.0'
    testImplementation 'org.sfvl:documentationtesting:1.2.0'
    testImplementation 'com.approvaltests:approvaltests:18.5.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.junit.platform:junit-platform-launcher:1.9.1'
    testImplementation 'com.github.javaparser:javaparser-core:3.24.7'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = '2021.2'
    plugins = ['com.intellij.java']
    updateSinceUntilBuild = false
}
patchPluginXml {
    changeNotes = """
      Change notes<br>
      <li>Switch between java/approved/received files</li>
      <li>Select several received files to approved</li>
      <li>Compare approved and received files from Java file</li>
      """
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/DocAsTest/doc-as-test-intellij-plugin"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
release {
    buildTasks = ["patchChangelog", "build"]
}